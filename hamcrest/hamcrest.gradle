plugins {
    id 'biz.aQute.bnd.builder' version '6.4.0'
}

version = rootProject.version

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named("test") {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Vendor': 'hamcrest.org',
                'Implementation-Version': version,
                'Automatic-Module-Name': 'org.hamcrest'
    }
    bundle {
        bnd 'Bundle-Name':          'org.hamcrest',
            'Bundle-SymbolicName':  'org.hamcrest',
            'Bundle-License':       'BSD-3-Clause',
            'Import-Package':       'javax.xml.namespace; resolution:=optional,' +
                                    'javax.xml.xpath;resolution:=optional,' +
                                    'org.w3c.dom;resolution:=optional,' +
                                    '*',
            '-exportcontents':      'org.hamcrest.*'
    }
}

javadoc {
    title = "Hamcrest ${version} API"
    exclude "org/hamcrest/internal/*"
    options.overview = file("javadoc-overview.html")
}
